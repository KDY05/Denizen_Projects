# 폐기
# 몹 크리퍼화 스크립트
# 사용 방법: /cpmob on|off
# 기능: 모든 몹(몬스터, 동물, 주민 등)이 크리퍼와 같이 행동합니다. 드랍 아이템은 유지됩니다.
# Script written by 어라랍(https://github.com/KDY05)

cpmob_init:
    type: world
    events:
        on scripts loaded:
        - if !<server.has_flag[cpmob]>:
            - flag server cpmob:false
        - flag server undeads:<list[zombie|zombie_villager|drowned|skeleton|stray|phantom]>

cpmob_world:
    type: world
    enabled: <server.flag[cpmob]>
    events:
        on delta time secondly:
        # 플레이어 주변의 몹을 감지하여 제거하고, 원래 모습으로 변장한 크리퍼를 다시 소환합니다. 체력은 상속됩니다.
        - foreach <server.online_players> as:origin:
            - foreach <[origin].location.find_entities.within[32]> as:entity:
                - if ( <[entity].is_mob> ) && ( !<[entity].libsdisguise_is_disguised> ) && ( <[entity].entity_type> != ENDER_DRAGON ):
                    # 청크 unload시 변장이 풀리게 되는 문제를 해결하기 위해 변장한 적이 있는 몹에는 disguised 태그를 줍니다.
                    - if !<[entity].has_flag[disguised]>:
                        - define etype <[entity].entity_type>
                        - define health <[entity].attribute_base_value[generic_max_health]>
                        - if <[entity].is_monster>:
                            - spawn creeper <[entity].location> save:sentity
                        - else:
                            - spawn creeper <[entity].location> persistent save:sentity
                        - remove <[entity]>
                        - libsdisguise mob type:<[etype]> target:<entry[sentity].spawned_entity>
                        - health <entry[sentity].spawned_entity> <[health]> heal
                        - flag <entry[sentity].spawned_entity> disguised:true
                        - flag <entry[sentity].spawned_entity> etype:<[etype]>
                        # 가스트 비행 기능
                        - if <[etype]> == ghast:
                            - fly <entry[sentity].spawned_entity> origin:<entry[sentity].spawned_entity.location> destinations:<[origin].location> speed:1.6
                            - wait 1.8s
                            - explode power:5 <entry[sentity].spawned_entity.location> breakblocks source:<entry[sentity].spawned_entity>
                            - kill <entry[sentity].spawned_entity>
                    - else:
                        - libsdisguise mob type:<[entity].flag[etype]> target:<[entity]>
                # 언데드 햇빛 발화 구현
                - if ( <[entity].location.world.is_day> ) && ( <[entity].has_flag[etype]> ):
                    - if ( <server.flag[undeads].contains[<[entity].flag[etype]>]> ) && ( <[entity].location.highest.y> <= <[entity].location.y.sub[1]> ):
                        - burn <[entity]> duration:8
        on creeper dies:
        # 드랍템 유지: 변장하기 전 엔티티 타입을 저장(flag)해두고 크리퍼가 죽을 때 저장해둔 엔티티를 같은 위치에서 죽게하여 구현.
        - if <context.entity.has_flag[disguised]>:
            #- narrate <entity[<context.entity.flag[etype]>].translated_name> targets:<server.online_players>
            - spawn <context.entity.flag[etype]> <context.entity.location> save:sentity
            - invisible <entry[sentity].spawned_entity>
            - hurt 999 <entry[sentity].spawned_entity> cause:ENTITY_ATTACK source:<context.damager>
            - determine NO_DROPS
        on player dies:
        # 데스 메시지 출력
        - if <context.damager.entity_type> == creeper:
            - announce "<context.entity.name>이(가) <entity[<context.damager.flag[etype]>].translated_name>에게 폭파당했습니다"
            - determine NO_MESSAGE
        on slime splits:
        # libsdisguise 오류 때문에 슬라임 분열을 금지시켜둠.
        - determine cancelled

cpmob_command:
    type: command
    name: cpmob
    description: cpmob command
    usage: /cpmob on|off
    tab completions:
        1: on|off
    script:
    - if <context.args.is_empty>:
            - narrate "<&e>/cpmob on|off" targets:<player>
            - stop
    - else:
        - choose <context.args.first>:
            - case on:
                - flag server cpmob:true
                - reload scripts_now
                - narrate "<&2>몹 크리퍼화를 활성화합니다." targets:<server.online_players>
                - stop
            - case off:
                - flag server cpmob:false
                - reload scripts_now
                - narrate "<&c>몹 크리퍼화를 비활성화합니다." targets:<server.online_players>
                - stop
            - default:
                - narrate "<&e>/cpmob on|off" targets:<player>